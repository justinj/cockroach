# =============================================================================
# join.opt contains exploration rules for the Join operator.
# =============================================================================


# CommuteJoin creates a Join with the left and right inputs swapped. This is
# useful for other rules that convert joins to other operators (like merge
# join).
[CommuteJoin, Explore]
(InnerJoin | FullJoin
  $left:*
  $right:*
  $on:*
)
=>
((OpName) $right $left $on)

# CommuteLeftJoin creates a Join with the left and right inputs swapped.
[CommuteLeftJoin, Explore]
(LeftJoin
  $left:*
  $right:*
  $on:*
)
=>
(RightJoin $right $left $on)

# CommuteRightJoin creates a Join with the left and right inputs swapped.
[CommuteRightJoin, Explore]
(RightJoin
  $left:*
  $right:*
  $on:*
)
=>
(LeftJoin $right $left $on)

# AssociateJoin applies the rule of join associativity. It converts an
# expression like:
#   A JOIN (B JOIN C ON B.x = C.x) ON A.y = B.y
# to the logically equivalent expression:
#   (A JOIN B ON A.y = B.y) JOIN C ON B.x = C.x
# It applies as long as the outer predicate is not True (we want to avoid
# pushing cross joins down), and the outer predicate only references one of
# the two inner relations. (Although this rule only allows references to the
# left inner relation, the CommuteJoin rule will effectively enable references
# to the right inner relation as well.)
[AssociateJoin, Explore]
(InnerJoin
    $left:*
    (InnerJoin
        $innerLeft:*
        $innerRight:*
        $innerOn:*
    )
    $on:*
)
=>
(SwapJoinsIfNeeded
    (InnerJoin
        (InnerJoin
            $left
            $innerLeft
            (ExtractBoundConditions $on (OutputCols2 $left $innerLeft))
        )
        $innerRight
        (ConcatFilters
            (ExtractUnboundConditions $on (OutputCols2 $left $innerLeft))
            $innerOn
        )
    )
)

# # AssociateJoin applies the rule of join associativity. It converts an
# # expression like:
# #   A JOIN (B JOIN C ON B.x = C.x) ON A.y = B.y
# # to the logically equivalent expression:
# #   (A JOIN B ON A.y = B.y) JOIN C ON B.x = C.x
# # It applies as long as the outer predicate is not True (we want to avoid
# # pushing cross joins down), and the outer predicate only references one of
# # the two inner relations. (Although this rule only allows references to the
# # left inner relation, the CommuteJoin rule will effectively enable references
# # to the right inner relation as well.)
# [AssociateJoin, Explore]
# (InnerJoin
#     $left:*
#     (InnerJoin
#         $innerLeft:*
#         $innerRight:*
#         $innerOn:*
#     )
#     $on:[
#         ...
#         $item:* & (IsBoundBy $item (OutputCols2 $left $innerLeft))
#         ...
#     ]
# )
# =>
# (SwapJoinsIfNeeded
#     (InnerJoin
#         (InnerJoin
#             $left
#             $innerLeft
#             (ExtractBoundConditions $on (OutputCols2 $left $innerLeft))
#         )
#         $innerRight
#         (ConcatFilters
#             (ExtractUnboundConditions $on (OutputCols2 $left $innerLeft))
#             $innerOn
#         )
#     )
# )

# GenerateMergeJoins creates MergeJoin operators for the join, using the
# interesting orderings property.
[GenerateMergeJoins, Explore]
(JoinNonApply $left:* $right:* $on:*)
=>
(GenerateMergeJoins (OpName) $left $right $on)

# GenerateLookupJoins creates LookupJoin operators for all indexes (of the Scan
# table) which allow it (including non-covering indexes). See the
# GenerateLookupJoins custom function for more details.
[GenerateLookupJoins, Explore]
(InnerJoin | LeftJoin
    $left:*
    (Scan $scanPrivate:*) & (IsCanonicalScan $scanPrivate)
    $on:*
)
=>
(GenerateLookupJoins (OpName) $left $scanPrivate $on)

# GenerateZigzagJoins creates ZigzagJoin operators for all index pairs (of the
# Scan table) where the prefix column(s) of both indexes is/are fixed to
# constant values in the filters. See comments in GenerateZigzagJoin and
# distsqlrun/zigzagjoiner.go for more details on when a zigzag join can be
# planned.
[GenerateZigzagJoins, Explore]
(Select
    (Scan $scan:*) & (IsCanonicalScan $scan)
    $filters:*
)
=>
(GenerateZigzagJoins $scan $filters)

# GenerateLookupJoinWithFilter creates a LookupJoin alternative for a Join which
# has a Select->Scan combination as its right input. The filter can get merged
# with the ON condition (this is correct for both inner and left join).
[GenerateLookupJoinsWithFilter, Explore]
(InnerJoin | LeftJoin
    $left:*
    (Select
        (Scan $scanPrivate:*) & (IsCanonicalScan $scanPrivate)
        $filters:*
    )
    $on:*
)
=>
(GenerateLookupJoins (OpName) $left $scanPrivate (ConcatFilters $on $filters))
